#!/usr/bin/perl

use strict;
use warnings;

use Getopt::Long;
use Pod::Usage;

use FindBin qw($RealBin);
use lib "$RealBin/../lib";
use BrownCS::UDB;
use BrownCS::UDB::Util qw(:all);

# Print a simple help message.
sub usage {
  my ($exit_status) = @_;
  pod2usage({ -exitval => $exit_status, -verbose => 1});
}

my $help = 0;
my $verbose = 0;
my $dryrun = 0;
my $username = $ENV{'USER'};

GetOptions ('help|h|?' => \$help, 
            'v|verbose' => \$verbose,
            'n|dryrun' => \$dryrun,
            'u|username=s' => \$username) or usage(1);
usage(0) if $help;

my $udb = BrownCS::UDB->new;
$udb->start($username);

if (@ARGV != 2) {
  usage(2);
}

my $name = shift @ARGV;
my $os = shift @ARGV;

my $class = $udb->db->resultset('CompClasses')->search({name => $name, os => $os})->first;
die "No such class $name for OS $os.\n" if not $class;

my @comps = $class->computers()->get_column("name")->all;
print join(" ", @comps) . "\n";

__END__

=head1 NAME

udb-show-class - Print out a list of all computers in a class

=head1 SYNOPSIS

udb-show-class [-u username] <class> <os>

=head1 DESCRIPTION

udb-show-class queries the UDB database for all computers that belong to
a given class and OS.

=head1 OPTIONS

=over

=item B<-h>, B<--help>

Print a help message and exit.

=item B<-u>, B<--username>=user

Logs onto the database server as the specified username, instead of as
the current user.

=back

=head1 AUTHORS

Aleks Bromfield.

=head1 SEE ALSO

B<udb>

=cut

