#!/usr/bin/perl

use strict;
use warnings;

use Getopt::Long;
use Pod::Usage;

use FindBin qw($RealBin);
use lib "$RealBin/../lib";
use BrownCS::udb::Schema;
use BrownCS::udb::Console qw(:all);

# Print a simple help message.
sub usage {
  my ($exit_status) = @_;
  pod2usage({ -exitval => $exit_status, -verbose => 1});
}

my $help = 0;

GetOptions (
  'help|h|?' => \$help, 
) or usage(1);
usage(0) if $help;

my $udb = BrownCS::udb::Schema->connect;
my $uc = new BrownCS::udb::Console(udb => $udb);

if (@ARGV != 2) {
  usage(2);
}

my $name = shift @ARGV;
my $os = shift @ARGV;

my $class = $udb->resultset('CompClasses')->search({name => $name, os_type => $os})->first;
die "No such class $name for OS $os.\n" if not $class;

my @comps = $class->computers->get_column("me.device_name")->all;
print join(" ", @comps) . "\n";

__END__

=head1 NAME

udb-show-class - Print out a list of all computers in a class

=head1 SYNOPSIS

udb-show-class <class> <os>

=head1 DESCRIPTION

udb-show-class queries the UDB database for all computers that belong to
a given class and OS.

=head1 OPTIONS

=over

=item B<-h>, B<--help>

Print a help message and exit.

=back

=head1 AUTHORS

Aleks Bromfield.

=head1 SEE ALSO

B<udb>

=cut

