#!/usr/bin/perl

use strict;
use warnings;

use Getopt::Long;
use Pod::Usage;

use FindBin qw($RealBin);
use lib "$RealBin/../lib";
use BrownCS::UDB;
use BrownCS::UDB::Util qw(:all);
use BrownCS::UDB::Verify qw(verify_hostname);

# Print a simple help message.
sub usage {
  my ($exit_status) = @_;
  pod2usage({ -exitval => $exit_status, -verbose => 1});
}

my $help = 0;
my $verbose = 0;
my $dryrun = 0;
my $username = $ENV{'USER'};

GetOptions ('help|h|?' => \$help, 
            'v|verbose' => \$verbose,
            'n|dryrun' => \$dryrun,
            'u|username=s' => \$username) or usage(1);
usage(0) if $help;

my $udb = BrownCS::UDB->new;
$udb->start($username);

if ((@ARGV < 1) or (@ARGV > 2)) {
  usage(2);
}

my $old_name = shift @ARGV;
my $new_name = shift @ARGV;

my $device = $udb->db->resultset('Equipment')->find($old_name);
die "No record for device $old_name.\n" if not $device;

if ($device->protected) {
  print "This device is protected!\n";
  print "Do not modify or delete this entry unless you know what you're doing.\n";
  exit(1);
}

if (not $new_name) {
  $new_name = get_new(0, "name", \&verify_hostname);
} elsif (not verify_hostname($new_name)) {
  exit(1);
}

if (not $new_name) {
  exit(0);
}

$device->name($new_name);
$device->update;

__END__

=head1 NAME

udb-rename - Perform maintenance on an existing device record in UDB

=head1 SYNOPSIS

udb-rename [-u username] <device> [<new-name>]

=head1 DESCRIPTION

Perform maintenance on an existing device record in UDB.

=head1 OPTIONS

=over

=item B<-h>, B<--help>

Print a help message and exit.

=item B<-u>, B<--username>=user

Logs onto the database server as the specified username, instead of as
the current user.

=back

=head1 AUTHORS

Aleks Bromfield.

=head1 SEE ALSO

B<udb>

=cut

