#!/usr/bin/perl

use strict;
use warnings;

use Getopt::Long;
use Pod::Usage;
use Data::Dumper;
use NetAddr::IP qw(Coalesce);
use List::MoreUtils qw(uniq);
use Template;
use Template::Stash;

use FindBin qw($RealBin);
use lib "$RealBin/../lib";
use BrownCS::UDB;
use BrownCS::UDB::Util qw(ask_password get_date fmt_time ipv4_n2x);

# Print a simple help message.
sub usage {
  my ($exit_status) = @_;
  pod2usage({ -exitval => $exit_status, -verbose => 1});
}

my $help = 0;
my $username = $ENV{'USER'};
my $udb = BrownCS::UDB->new;
my $dry_run = 1;
my $verbose = 0;

GetOptions ('help|h|?' => \$help, 
            'n|dryrun' => \$dry_run,
            'v|verbose' => \$verbose,
            'u' => \$username) or usage(2);
usage(1) if $help;

if (@ARGV > 0) {
  usage(2);
}

my $password = &ask_password;

$udb->start($username, $password);

my $tt = Template->new({INCLUDE_PATH => "$RealBin/../templates"}) || die "$Template::ERROR\n";

&build_tftpboot;
&build_netgroup;
&build_dhcp;
&build_nagios_hosts;
&build_nagios_services;
&build_wpkg_hosts;
&build_dns;
&build_ypmake;

END {
  $udb->finish;
}

sub maybe_system {
  if ($dry_run) {
    print "DEBUG: @_\n";
  } else {
    system(@_);
  }
}

sub maybe_rename {
  my ($old, $new) = @_;
  if ($dry_run) {
    print "DEBUG: rename $old to $new\n";
  } else {
    die "$0: ERROR: Failed to rename $new: $!\n" if(!rename("$old", "$new"));
  }
}

sub build_tftpboot {

  my $tftpboot_path = "/tftpboot/pxelinux.cfg";

  my $sth = $udb->prepare("select na.ipaddr, c.hw_arch, c.os, c.pxelink from net_addresses na, net_interfaces ni, net_addresses_net_interfaces nani, computers c where ni.equip_name = c.name and nani.net_addresses_id = na.id and nani.net_interfaces_id = ni.id");

  $sth->execute();
  my $array_ref = $sth->fetchall_arrayref({});

  foreach my $addr (@{$array_ref}) {

    my $bootimage;

    next if not defined $addr->{os};
    next if not defined $addr->{hw_arch};
    next if not defined $addr->{ipaddr};

    if (defined $addr->{pxelink}) {
      $bootimage = $addr->{pxelink};
    } elsif ($addr->{os} eq 'linux') {
      $bootimage = ("fai-workstation-" . $addr->{hw_arch});
    } elsif ($addr->{os} eq 'linux-server') {
      $bootimage = ("fai-server-" . $addr->{hw_arch});
    }
      
    next if not defined($bootimage);

    my $hex_ip = ipv4_n2x($addr->{ipaddr});

    if($dry_run) {
      print "DEBUG: $hex_ip -> $bootimage\n";
    } else {
      unlink("$tftpboot_path/$hex_ip");
      symlink("$bootimage", "$tftpboot_path/$hex_ip") ||
        warn "$0: ERROR: Failed to create link $hex_ip: $!\n";
    }
  }
}

sub add_to_group {
  my ($netgroups, $grp, $host) = @_;

  if(defined($netgroups->{$grp})) {
    $netgroups->{$grp} .= " (${host},,)";
  } else {
    $netgroups->{$grp} = "(${host},,)";
  }

}

sub build_netgroup {
  my(%netgroups) = ();
  my $host_classes = $udb->get_host_class_map;

  my $sth = $udb->prepare("select e.name, c.os, e.managed_by from equipment e, computers c where c.name = e.name");
  $sth->execute();
  my $array_ref = $sth->fetchall_arrayref({});

  foreach my $host (@{$array_ref}) {

    my $hostname = $host->{name};
    my $manager = $host->{managed_by};
    my $os = $host->{os};

    if (($manager eq 'tstaff') and (defined $os) and ($os =~ /^(?:linux|linux-server|solaris)$/)) {
      add_to_group(\%netgroups, "trusted", $hostname);
    }

    my $classes_ref = $host_classes->{$hostname};
    next unless defined $classes_ref;
    my @classes = @{$classes_ref};

    for (@classes) {

      if (/^camera$/) {
        add_to_group(\%netgroups, "camera", $hostname);
      } elsif (/^cgc$/) {
        add_to_group(\%netgroups, "cgc", $hostname);
      } elsif (/^graphics$/) {
        add_to_group(\%netgroups, "graphics", $hostname);
      } elsif (/^fun$/) {
        add_to_group(\%netgroups, "ugrad", $hostname);
      } elsif (/^ssh\.forward$/) {
        add_to_group(\%netgroups, "sunlab", $hostname);
      } elsif (/^tstaff-netgroup$/) {
        add_to_group(\%netgroups, "tstaff", $hostname);
      } elsif (/^thermo$/) {
        add_to_group(\%netgroups, "thermo", $hostname);
      } elsif (/^liebert$/) {
        add_to_group(\%netgroups, "liebert", $hostname);
      } elsif (/^server$/) {
        add_to_group(\%netgroups, "server", $hostname);
      }
    }
  }

  foreach my $grp (sort(keys(%netgroups))) {
    my $val = $netgroups{$grp};
    maybe_system("/tstaff/bin/ldap-netgroup -e '$val' set $grp 2>/dev/null >/dev/null");
  }

}

sub build_dhcp {

  my $file = $dry_run ? '/tmp/dhcpd.conf' : '/maytag/sys0/dhcp/dhcpd.conf';
  my $PATH_TMPFILE = $file . '.tmp';
  my $vars = {filename => $file, date => get_date(), dbh => $udb->{dbh}};
  $tt->process('dhcpd.conf.tt2', $vars, $PATH_TMPFILE) || die $tt->error(), "\n";

  # send new config file to each server
  maybe_rename($PATH_TMPFILE, $file);
  my @CDB_DHCP_SERVERS = qw(payday snickers);
  foreach my $host (@CDB_DHCP_SERVERS) {
    maybe_system('scp', '-pq', $file, "$host:/etc");
    if ( $? != 0 ) {
      warn "$0: ERROR: Failed to copy DNS files to $host\n";
    }
  }
  maybe_system('ssh', '-x', 'dhcp', '/etc/init.d/dhcp restart');
}

sub build_nagios_hosts {

  my $file = $dry_run ? '/tmp/hosts.cfg' : '/maytag/sys0/Linux/files/add/group.debian.server.nagios3/etc/nagios3/conf.d/hosts.cfg';
  my $PATH_TMPFILE = $file . '.tmp';
  my $vars = {filename => $file, date => get_date(), dbh => $udb->{dbh}};
  $tt->process('hosts.cfg.tt2', $vars, $PATH_TMPFILE) || die $tt->error(), "\n";

  # send new config file to each server
  maybe_rename($PATH_TMPFILE, $file);
  maybe_system('scp', '-pq', $file, "storm:/etc/nagios3/conf.d/");
  if ( $? != 0 ) {
    warn "$0: ERROR: Failed to copy nagios files to storm\n";
  }
  maybe_system('ssh', '-x', 'storm', '/etc/init.d/nagios3 restart');
}

sub build_nagios_services {

  my $file = $dry_run ? '/tmp/services.cfg' : '/maytag/sys0/Linux/files/add/group.debian.server.nagios3/etc/nagios3/conf.d/services.cfg';
  my $PATH_TMPFILE = $file . '.tmp';
  my $vars = {filename => $file, date => get_date(), dbh => $udb->{dbh}};
  $tt->process('services.cfg.tt2', $vars, $PATH_TMPFILE) || die $tt->error(), "\n";

  # send new config file to each server
  maybe_rename($PATH_TMPFILE, $file);
  maybe_system('scp', '-pq', $file, "storm:/etc/nagios3/conf.d/");
  if ( $? != 0 ) {
    warn "$0: ERROR: Failed to copy nagios files to storm\n";
  }

  maybe_system('ssh', '-x', 'storm', '/etc/init.d/nagios3 restart');

}

sub build_wpkg_hosts {

  my $file = $dry_run ? '/tmp/wpkg-hosts.xml' : '/u/system/win32/WPKG/hosts/cdb.xml';
  my $PATH_TMPFILE = $file . '.tmp';

  my $vars = {
    filename => $file,
    date => get_date(),
    hosts => [],
  };

  my $host_classes = $udb->get_host_class_map;

  my $sth = $udb->prepare("select e.name, c.os from equipment e, computers c where c.name = e.name and e.managed_by = 'tstaff'");
  $sth->execute();
  my $array_ref = $sth->fetchall_arrayref({});

  foreach my $host (@{$array_ref}) {

    my $host_var = {};

    my $hostname = $host->{name};
    $host_var->{hostname} = $hostname;

    my $os = $host->{os};

    next unless((defined $os) and (($os eq 'winxp') or ($os eq 'vista')));

    # generate profile list

    my @wpkg_profiles = ();

    my $classes_ref = $host_classes->{$hostname};
    next unless defined $classes_ref;
    my @classes = @{$classes_ref};

    if ($os eq 'winxp') {
      push @wpkg_profiles, "winxp";
    } elsif ($os eq 'vista') {
      push @wpkg_profiles, "vista";
    }

    for (@classes) {

      if (/^desktop$/) {
        push @wpkg_profiles, "desktop";
        push @wpkg_profiles, "$os-dept";
      }

      if (/^classroom$/) {
        push @wpkg_profiles, "classroom";
      }

      if (/^maya$/) {
        push @wpkg_profiles, "maya";
      }

      if (/^tstaff$/) {
        push @wpkg_profiles, "tstaff";
      }

      if (/^afs$/) {
        push @wpkg_profiles, "afs";
      }

      if (/^fun$/) {
        push @wpkg_profiles, "fun";
      }

      if (/^research$/) {
        push @wpkg_profiles, "research";
        push @wpkg_profiles, "$os-research";
      }

      if (/^server$/) {
        push @wpkg_profiles, "server";
      }

      if (/^remote$/) {
        push @wpkg_profiles, "desktop";
        push @wpkg_profiles, "remote";
        push @wpkg_profiles, "$os-remote";
      }

      if (/^laptop$/) {
        push @wpkg_profiles, "$os-laptop";
      }

      if (/^laptop.loaner$/) {
        push @wpkg_profiles, "$os-laptop";
        push @wpkg_profiles, "loaner-laptop";
      }

      if (/^laptop.x61$/) {
        push @wpkg_profiles, "$os-laptop";
        push @wpkg_profiles, "x61";
      }

      # licensed software

      if (/^adobe-ae-pp$/) {
        push @wpkg_profiles, "adobe-ae-pp";
      }

      if (/^framemaker$/) {
        push @wpkg_profiles, "framemaker";
      }

      if (/^powerdvd$/) {
        push @wpkg_profiles, "powerdvd";
      }

    }

    next unless @wpkg_profiles;

    my $first_profile = shift(@wpkg_profiles);

    $host_var->{first_profile} = $first_profile;
    $host_var->{other_profiles} = \@wpkg_profiles;

    push @{$vars->{hosts}}, $host_var;
  }

  $tt->process('wpkg-hosts.xml.tt2', $vars, $PATH_TMPFILE) || die $tt->error(), "\n";
  maybe_rename($PATH_TMPFILE, $file);
}

sub build_dns_map_forward {
  my ($serial_num, $domain) = @_;

  my @domain_parts = split(/\./, $domain);
  my $zone = $domain_parts[0];

  my $file = $dry_run ? "/tmp/db.$zone" : "/maytag/sys0/DNS/db.$zone";
  my $PATH_TMPFILE = $file . '.tmp';
  my $vars = {
    filename => $file,
    date => get_date(),
    dbh => $udb->{dbh},
    zone => $zone,
    serial_num => $serial_num,
    domain => $domain,
  };
  $tt->process('dns.db.forward.tt2', $vars, $PATH_TMPFILE) || die $tt->error(), "\n";

  return $file;
}

sub build_dns_map_reverse {
  my ($serial_num, $subnet) = @_;
  my $zone = $subnet->prefix;

  my $file = $dry_run ? "/tmp/db.$zone" : "/maytag/sys0/DNS/db.$zone";
  my $PATH_TMPFILE = $file . '.tmp';
  my $vars = {
    filename => $file,
    date => get_date(),
    dbh => $udb->{dbh},
    zone => $zone,
    serial_num => $serial_num,
    cidr => $subnet->cidr,
  };
  $tt->process('dns.db.reverse.tt2', $vars, $PATH_TMPFILE) || die $tt->error(), "\n";

  return $file;
}

sub build_dns {

  $Template::Stash::SCALAR_OPS->{fix_width} = sub {
    my ($str, $width) = @_;
    my $spaces = ' ' x ($width - length($str));
    return ($str . $spaces);
  };

  $Template::Stash::SCALAR_OPS->{to_dns_ptr_record} = sub {
    my ($ip) = @_;
    my $formatted_ip = join(".",reverse(split(/\./, $ip)));
    return "$formatted_ip.IN-ADDR.ARPA.";
  };

  # TODO increment the SOA line
  my ($serial_num) = $udb->{dbh}->selectrow_array("select nextval('dns_serial_num_seq')");

  my $sth = $udb->prepare("select nv.network from net_vlans nv, net_zones nz where nv.zone = nz.name and nz.routing_type != 'private' and nz.zone_manager = 'tstaff'");
  $sth->execute();
  my $array_ref = $sth->fetchall_arrayref({});

  my @spread_subnets = ();

  foreach my $subnet (@{$array_ref}) {
    push @spread_subnets, (new NetAddr::IP($subnet->{network}));
  }

  my $classC = Coalesce(24, 1, @spread_subnets);
  my $classB = Coalesce(16, 1, grep {$_->masklen < 24} @spread_subnets);
  my @subnets = uniq( @{$classC}, @{$classB} );

  my @files = ();

  foreach my $subnet (@subnets) {
    my $file = build_dns_map_reverse($serial_num, $subnet);
    push @files, $file;
  }

  my @domains = qw(cs.brown.edu ilab.cs.brown.edu);
  foreach my $domain (@domains) {
    my $file = build_dns_map_forward($serial_num, $domain);
    push @files, $file;
  }

  # fix permissions
  foreach my $file (@files) {
    maybe_rename("$file.tmp", $file);
    if ($dry_run) {
      print "DEBUG: fix permissions\n";
    } else {
      # fix permissions the file
      chown(0, (getgrnam('sys'))[2], $file) || warn "$0: WARNING: Failed to chown $file: $!\n";
      chmod(0444, $file) || warn "$0: WARNING: Failed to chmod $file: $!\n";
    }
  }

  # send new config file to each server
  my @dns_servers = qw(payday snickers);
  foreach my $host (@dns_servers) {
    maybe_system('scp', '-pq', @files, "$host:/var/cache/bind");
    if ( $? != 0 ) {
      warn "$0: ERROR: Failed to copy DNS files to $host\n";
    }

    maybe_system('ssh', '-x', $host, '/usr/sbin/rndc reload');
    if ( $? != 0 ) {
        warn "$0: ERROR: Failed to send DNS reload command to on $host\n";
    }
  }

}

sub build_ypmake {

  my $cmd;

  if($verbose) {
    $cmd = "/u/system/bin/ypmake";
  } else {
    $cmd = "/u/system/bin/ypmake >/dev/null"
  }

  maybe_system($cmd);

}

__END__

=head1 NAME

build - build system databases/config files from udb

=head1 SYNOPSIS

build [-u username]

=head1 DESCRIPTION

builds stuff

=head1 OPTIONS

=over

=item B<-h>, B<--help>

Print a help message and exit.

=item B<-u>, B<--username>=user

Logs onto the database server as the specified username, instead of as
the current user.

=item B<-n>, B<--dryrun>

Instead of modifying the system, print out a record of what would be
done. Some files are written to /tmp.

=item B<-v>, B<--verbose>

Print lots of text about what is going on.

=back

=head1 AUTHORS

Aleks Bromfield.

=head1 SEE ALSO

B<udb>

=cut

