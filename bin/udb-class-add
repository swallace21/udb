#!/usr/bin/perl

use strict;
use warnings;

use Getopt::Long;
use Pod::Usage;
use DBI qw(:sql_types);
use DBD::Pg qw(:pg_types);

use FindBin qw($RealBin);
use lib "$RealBin/../lib";
use BrownCS::UDB;
use BrownCS::UDB::Util qw(:all);

# Print a simple help message.
sub usage {
  my ($exit_status) = @_;
  pod2usage({ -exitval => $exit_status, -verbose => 1});
}

my $help = 0;
my $verbose = 0;
my $dryrun = 0;
my $username = $ENV{'USER'};

GetOptions ('help|h|?' => \$help, 
            'v|verbose' => \$verbose,
            'n|dryrun' => \$dryrun,
            'u|username=s' => \$username) or usage(1);
usage(0) if $help;

my $udb = BrownCS::UDB->new;
$udb->start($username);

if (@ARGV < 1) {
  usage(2);
}

my $class = shift @ARGV;
my @hostnames = @ARGV;

$udb->prepare("class_comp_insert", "INSERT INTO comp_classes_computers (comp_classes_id, computers_id) VALUES (?,?)");
$udb->{sths}->{class_comp_insert}->bind_param(1, undef, SQL_INTEGER);
$udb->{sths}->{class_comp_insert}->bind_param(2, undef, SQL_INTEGER);

my $class_id = $udb->get_class($class);

foreach my $hostname (@hostnames) {
  if ($verbose) {
    print "adding host $hostname to class $class\n";
  }
  my %host = $udb->get_host($hostname);
  $udb->{sths}->{class_comp_insert}->execute($class_id, $host{comp_id});
}

END {
  if (defined $udb) {
    $udb->finish;
  }
}

__END__

=head1 NAME

udb-class-add - Add one or more computers to a class

=head1 SYNOPSIS

udb-class-add [-u username] <classname> [<hostname> ...]

=head1 DESCRIPTION

adds one or more computers to a class

=head1 OPTIONS

=over

=item B<-h>, B<--help>

Print a help message and exit.

=item B<-u>, B<--username>=user

Logs onto the database server as the specified username, instead of as
the current user.

=back

=head1 AUTHORS

Aleks Bromfield.

=head1 SEE ALSO

B<udb>

=cut

