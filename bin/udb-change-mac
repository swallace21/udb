#!/usr/bin/perl

use strict;
use warnings;

use Getopt::Long;
use Pod::Usage;

use FindBin qw($RealBin);
use lib "$RealBin/../lib";
use BrownCS::UDB;
use BrownCS::UDB::Util qw(:all);
use BrownCS::UDB::Verify qw(verify_mac);

# Print a simple help message.
sub usage {
  my ($exit_status) = @_;
  pod2usage({ -exitval => $exit_status, -verbose => 1});
}

my $help = 0;
my $verbose = 0;
my $dryrun = 0;
my $username = $ENV{'USER'};

GetOptions ('help|h|?' => \$help, 
            'v|verbose' => \$verbose,
            'n|dryrun' => \$dryrun,
            'u|username=s' => \$username) or usage(1);
usage(0) if $help;

my $udb = BrownCS::UDB->new;
$udb->start($username);

if ((@ARGV < 1) or (@ARGV > 2)) {
  usage(2);
}

my $hostname = shift @ARGV;
my $new_mac = shift @ARGV;

if ($udb->is_protected($hostname)) {
  print "This device is protected!\n";
  print "Do not modify or delete this entry unless you know what you're doing.\n";
  exit(1);
}

my ($iface_count) = $udb->query("select count(id) from net_interfaces where \
  equip_name = ?", $hostname)->flat;

if ($iface_count < 1) {
  print "No networked device with this name exists.\n";
  exit(1);
} elsif ($iface_count > 1) {
  print "This device has more than one network card.\n";
  print "Do not modify or delete this entry unless you know what you're doing.\n";
  exit(1);
}

if (not $new_mac) {
  $new_mac = get_new(0, "MAC address", \&verify_mac);
} elsif (not verify_mac($new_mac)) {
  exit(1);
}

if (not $new_mac) {
  exit(0);
}

$udb->query("update net_interfaces set ethernet = ? where equip_name = ?", $new_mac, $hostname);

END {
  if (defined $udb) {
    $udb->finish;
  }
}

__END__

=head1 NAME

udb-modify - Perform maintenance on an existing device record in UDB

=head1 SYNOPSIS

udb-modify [-u username] <device>

=head1 DESCRIPTION

Perform maintenance on an existing device record in UDB.

=head1 OPTIONS

=over

=item B<-h>, B<--help>

Print a help message and exit.

=item B<-u>, B<--username>=user

Logs onto the database server as the specified username, instead of as
the current user.

=back

=head1 AUTHORS

Aleks Bromfield.

=head1 SEE ALSO

B<udb>

=cut

