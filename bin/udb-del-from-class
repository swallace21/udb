#!/usr/bin/perl

use strict;
use warnings;

use Getopt::Long;
use Pod::Usage;
use Data::Dumper;

use FindBin qw($RealBin);
use lib "$RealBin/../lib";
use BrownCS::UDB;
use BrownCS::UDB::Util qw(:all);

# Print a simple help message.
sub usage {
  my ($exit_status) = @_;
  pod2usage({ -exitval => $exit_status, -verbose => 1});
}

my $help = 0;
my $verbose = 0;
my $dryrun = 0;
my $username = $ENV{'USER'};

sub vprint {
  my ($msg) = @_;
  if ($verbose) {
    print $msg, "\n";
  }
}

GetOptions ('help|h|?' => \$help, 
            'v|verbose' => \$verbose,
            'n|dryrun' => \$dryrun,
            'u|username=s' => \$username) or usage(1);
usage(0) if $help;

my $udb = BrownCS::UDB->new;
$udb->start($username);

if (@ARGV < 1) {
  usage(2);
}

my $classname = shift @ARGV;
my @hostnames = @ARGV;

foreach my $hostname (@hostnames) {
  my $comp = $udb->db->resultset('Computers')->find($hostname);
  if (not $comp) {
    print "No record for computer $hostname.\n" if not $comp;
    next;
  }

  if ($comp->protected) {
    printf("The device %s is protected!\n", $comp->name);
    print "Do not modify or delete this entry unless you know what you're doing.\n";
    next;
  }

  my $class = $udb->db->resultset('CompClasses')->find({name => $classname, os => $comp->os});
  if (not $class) {
    vprint("no record for class $classname on OS " . $comp->os);
    next;
  }

  if ($class->computers->find($hostname)) {
    vprint("removing computer $hostname from class $classname");
    $comp->remove_from_classes($class);
  }

  if (not $class->computers->count) {
    vprint("removing empty class $classname on OS " . $comp->os);
    $class->delete;
  }
}

__END__

=head1 NAME

udb-del-from-class - Delete one or more computers from a class

=head1 SYNOPSIS

udb-del-from-class [-u username] <classname> [<hostname> ...]

=head1 DESCRIPTION

deletes one or more computers from a class

=head1 OPTIONS

=over

=item B<-h>, B<--help>

Print a help message and exit.

=item B<-u>, B<--username>=user

Logs onto the database server as the specified username, instead of as
the current user.

=back

=head1 AUTHORS

Aleks Bromfield.

=head1 SEE ALSO

B<udb>

=cut

