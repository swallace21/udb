#!/usr/bin/perl

use strict;
use warnings;

use Getopt::Long;
use Pod::Usage;

use FindBin qw($RealBin);
use lib "$RealBin/../lib";
use BrownCS::udb::Schema;
use BrownCS::udb::Util qw(:all);
use BrownCS::udb::Console qw(:all);

# Print a simple help message.
sub usage {
  my ($exit_status) = @_;
  pod2usage({ -exitval => $exit_status, -verbose => 1});
}

my $help = 0;
my $verbose = 0;
my $dryrun = 0;
my $username = $ENV{'USER'};

GetOptions ('help|h|?' => \$help, 
            'v|verbose' => \$verbose,
            'n|dryrun' => \$dryrun,
            'u|username=s' => \$username) or usage(1);
usage(0) if $help;

if (@ARGV != 1) {
  usage(2);
}

my $udb = BrownCS::udb::Schema->connect;
my $uc = new BrownCS::udb::Console(udb => $udb);

my $name = shift @ARGV;

eval {
  my $device = $udb->resultset('Devices')->find($name);
  my $comp = $device->computer;
  my @classes = $comp->comp_classes->get_column('name')->all;
  print join(' ',@classes) . "\n";
};
if ($@) {
  die "No record for computer $name.\n";
}

__END__

=head1 NAME

udb-classes - Print out the classes a computer belongs to

=head1 SYNOPSIS

udb-classes [-u username] <hostname>

=head1 DESCRIPTION

udb-classes queries the UDB database for the list of classes that a
computer belongs to, and prints it out to the console.

=head1 OPTIONS

=over

=item B<-h>, B<--help>

Print a help message and exit.

=item B<-u>, B<--username>=user

Logs onto the database server as the specified username, instead of as
the current user.

=back

=head1 AUTHORS

Aleks Bromfield.

=head1 SEE ALSO

B<udb>

=cut

