#!/usr/bin/perl

use strict;
use warnings;

use Getopt::Long;
use Getopt::Std;
use Pod::Usage;
use Data::Dumper;
use Date::Format;
use DateTime::Format::Pg;
use File::Temp;

use FindBin qw($RealBin);
use lib "$RealBin/../lib";
use BrownCS::udb::Schema;
use BrownCS::udb::Build;
use BrownCS::udb::Util qw(:all);

# Print a simple help message.
sub usage {
  my ($exit_status) = @_;
  pod2usage({ -exitval => $exit_status, -verbose => 1});
}

my $help = 0;
my $udb = BrownCS::udb::Schema->connect;
my $dryrun = 0;
my $verbose = 0;
my $root = 0;

BEGIN { $| = 1 }

GetOptions (
  'help|h|?' => \$help, 
  'n|dryrun' => \$dryrun,
  'v|verbose' => \$verbose,
) or usage(2);
usage(1) if $help;

if (@ARGV > 0) {
  usage(2);
}

my $ub = new BrownCS::udb::Build(udb => $udb, verbose => $verbose, dryrun => $dryrun);

if (not $dryrun) {

  if(not BrownCS::udb::Util::okay_adminhost){
    exit;
  }

  if(not BrownCS::udb::Util::okay_kerberos){
    exit;
  }
  
  if(BrownCS::udb::Util::okay_root_silent){
    print "ERROR: Do not run as root.\n";
    exit;
  }
  system("sudo -v");
}

my $rs;
eval {
  $rs = $udb->txn_do(sub {
    my $build_start = time2str("%Y-%L-%d %T", time);
    my %builds;
  
    $ub->build_lock;
  
    my $krbadmin = "udbuser/admin";
    my $keytab = tmpnam();
    $ub->get_keytab($krbadmin, $keytab);

    # delete anything marked for deletion since last successful build
    $ub->staged_deletions($krbadmin, $keytab, \%builds);
  
    # add anything staged for addition since last successful build
    $ub->staged_additions($krbadmin, $keytab, \%builds);

    # add anything staged for modification since last successful build
    $ub->staged_modifications($krbadmin, $keytab, \%builds);

    # build service and data files
    $ub->build_tftpboot;
    $ub->build_netgroup;
    $ub->build_dhcp;
    $ub->build_nagios;
    $ub->build_wpkg_hosts;
    $ub->build_dns;
    $ub->build_finger_data;

    my $count = unlink $keytab;
    if ($count != 1) {
      print "WARNING: unable to unlink keytab file \"$keytab\", please delete by hand\n";
    }

    $ub->update_build_times($build_start, \%builds);

    $ub->build_unlock;
  });
};
if ($@) {
  $ub->build_unlock;
  die <<EOF;
Operation failed.  Details:
  $@
EOF
}

__END__

=head1 NAME

build - build system databases/config files from udb

=head1 SYNOPSIS

build

=head1 DESCRIPTION

builds stuff

=head1 OPTIONS

=over

=item B<-h>, B<--help>

Print a help message and exit.

=item B<-n>, B<--dryrun>

Don't make any changes; just print what would be done. Files which would
normally be copied to servers are instead left in /tmp. 

=item B<-v>, B<--verbose>

Print lots of text about what is going on.

=back

=head1 AUTHORS

Aleks Bromfield
Mark Dieterich

=head1 SEE ALSO

B<udb>

=cut

