#!/usr/bin/perl

use strict;
use warnings;

use Getopt::Long qw(:config pass_through);
use Pod::Usage;

use FindBin qw($RealBin);

my %udb_commands = (
  'add' => {
    'desc' => ["\t\tadd a new host"],
    'common' => 1,
  },
  'class-add' => {
    'desc' => ["\tadd one or more hosts to a class"],
    'common' => 0,
  },
  'class-del' => {
    'desc' => ["\tremove one or more hosts from a class"],
    'common' => 0,
  },
  'classes' => {
    'desc' => ["\tlist the classes to which the host belongs"],
    'common' => 0,
  },
  'class-show' => {
    'desc' => ["\tlist the members of a class"],
    'common' => 0,
  },
  'contact-list' => {
    'desc' => ["print out a contact list for all hosts in a class"],
    'common' => 0,
  },
  'delete' => {
    'desc' => ["\tdelete a host"],
    'common' => 1,
  },
  'modify' => {
    'desc' => ["\tmodify a host"],
    'common' => 1,
  },
  'show' => {
    'desc' => ["\tprint a summary of a host"],
    'common' => 1,
  },
);

# Print a simple help message.
sub usage {
  my ($exit_status, $long) = @_;
  pod2usage({ -exitval => "NOEXIT", -verbose => 1});
  print "Commands:\n\n";
  foreach my $k (sort(keys(%udb_commands))) {
    if (not $long) {
      next if not $udb_commands{$k}{'common'};
    }
    print "    $k\t" . $udb_commands{$k}{'desc'}[0] . "\n";
  }
  print "\nSee 'udb help COMMAND' for more information on a specific command.\n";
  exit($exit_status);
}

my $help = 0;
my $verbose = 0;
my $dryrun = 0;
my $username = $ENV{'USER'};

GetOptions ('help|h|?' => \$help) or usage(1, 0);

my $cmd = shift;

if (not defined $cmd) {
  usage(2, 0);
}

if (($cmd eq 'help') or $help) {
  if (@ARGV or $help) {
    if ($cmd eq 'help') {
      $cmd = shift;
    }
    unshift(@ARGV, '-h');
  } else {
    usage(0, 1);
  }
}

if (not defined($udb_commands{$cmd})) {
  usage(2);
}

exec("$RealBin/udb-$cmd", @ARGV);

__END__

=head1 NAME

udb - main interface to the Universal DataBase

=head1 SYNOPSIS

udb [-v] [-n] [-u username] <command> [<arg> ...]

=head1 DESCRIPTION

Wrapper script for running other udb commands.

=head1 OPTIONS

=over

=item B<-h>, B<--help>

Print a help message and exit.

=item B<-u>, B<--username>=user

Logs onto the database server as the specified username, instead of as
the current user.

=item B<-v>, B<--verbose>

Print lots of information about what udb is doing.

=item B<-n>, B<--dryrun>

Don't make any changes; just print what would be done.

=back

=head1 AUTHORS

Aleks Bromfield.

=cut

