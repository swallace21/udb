#!/usr/bin/perl

use strict;
use warnings;

use Getopt::Long;
use Pod::Usage;

use FindBin qw($RealBin);
use lib "$RealBin/../lib";
use BrownCS::UDB;
use BrownCS::UDB::Util qw(ask_password);

use BrownCS::UDB::Frontend::Add qw(add);
use BrownCS::UDB::Frontend::ClassAdd qw(class_add);
use BrownCS::UDB::Frontend::ClassDel qw(class_del);
use BrownCS::UDB::Frontend::Classes qw(classes);
use BrownCS::UDB::Frontend::ClassShow qw(class_show);
use BrownCS::UDB::Frontend::ContactList qw(contact_list);
use BrownCS::UDB::Frontend::Del qw(del);
use BrownCS::UDB::Frontend::Mod qw(mod);
use BrownCS::UDB::Frontend::Show qw(show);

my %udb_commands = (
  'add' => {
    'fn' => \&add,
    'desc' => ["\t\tadd a new host"],
    'usage' => "add",
    'common' => 1,
  },
  'class-add' => {
    'fn' => \&class_add,
    'desc' => ["\tadd one or more hosts to a class"],
    'usage' => "class-add CLASS HOST...",
    'common' => 0,
  },
  'class-del' => {
    'fn' => \&class_del,
    'desc' => ["\tremove one or more hosts from a class"],
    'usage' => "class-del CLASS HOST...",
    'common' => 0,
  },
  'classes' => {
    'fn' => \&classes,
    'desc' => ["\tlist the classes to which the host belongs"],
    'usage' => "classes HOST",
    'common' => 0,
  },
  'class-show' => {
    'fn' => \&class_show,
    'desc' => ["\tlist the members of a class"],
    'usage' => "class-show CLASS",
    'common' => 0,
  },
  'contact-list' => {
    'fn' => \&contact_list,
    'desc' => ["print out a contact list for all hosts in a class"],
    'usage' => "contact-list CLASS",
    'common' => 0,
  },
  'delete' => {
    'fn' => \&del,
    'desc' => ["\tdelete a host"],
    'usage' => "delete HOST",
    'common' => 1,
  },
  'help' => {
    'fn' => \&command_help,
    'desc' => ["\tget help on a command"],
    'usage' => "help command",
    'common' => 0,
  },
  'modify' => {
    'fn' => \&mod,
    'desc' => ["\tmodify a host"],
    'usage' => "modify HOST",
    'common' => 1,
  },
  'show' => {
    'fn' => \&show,
    'desc' => ["\tprint a summary of a host"],
    'usage' => "show HOST",
    'common' => 1,
  },
);

# Print a simple help message.
sub usage {
  my ($exit_status) = @_;
  pod2usage({ -exitval => "NOEXIT", -verbose => 1});
  print "Commands:\n\n";
  foreach my $k (sort(keys(%udb_commands))) {
    next if not $udb_commands{$k}{'common'};
    print "    $k\t" . $udb_commands{$k}{'desc'}[0] . "\n";
  }
  print "\nSee 'udb help COMMAND' for more information on a specific command.\n";
  exit($exit_status);
}

# Print a simple help message for a specific command.
sub command_help {
  my ($cmd) = @_;
  print "Usage: ";
  print $udb_commands{$cmd}{"usage"} , "\n\n";
  my $lines = $udb_commands{$cmd}{"desc"};
  foreach my $line (@$lines) {
    $line =~ s/^\s+//;
    print "  \u$line.\n";
  }
  print "\n";
}

my $help = 0;
my $verbose = 0;
my $dryrun = 0;
my $username = $ENV{'USER'};

GetOptions ('help|h|?' => \$help, 
            'v|verbose' => \$verbose,
            'n|dryrun' => \$dryrun,
            'u' => \$username) or usage(2);
usage(1) if $help;

my $cmd = shift;

if (not defined $cmd) {
  usage(2);
}

my $fn;

if(defined($udb_commands{$cmd})) {
  $fn = $udb_commands{$cmd}{'fn'};
} else {
  usage(2);
}

my $password = &ask_password;

my $udb = BrownCS::UDB->new;
$udb->start($username, $password);

$fn->($udb, $verbose, $dryrun, @ARGV);

END {
  if (defined $udb) {
    $udb->finish;
  }
}

__END__

=head1 NAME

udb - main interface to the Universal DataBase

=head1 SYNOPSIS

udb [-v] [-n] [-u username] command [args ...]

=head1 DESCRIPTION

it's like cdb but better

=head1 OPTIONS

=over

=item B<-h>, B<--help>

Print a help message and exit.

=item B<-u>, B<--username>=user

Logs onto the database server as the specified username, instead of as
the current user.

=item B<-v>, B<--verbose>

Print lots of information about what udb is doing.

=item B<-n>, B<--dryrun>

Don't make any changes; just print what would be done.

=back

=head1 AUTHORS

Aleks Bromfield.

=cut

